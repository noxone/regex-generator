import org.jetbrains.kotlin.gradle.targets.js.yarn.YarnPlugin
import org.jetbrains.kotlin.gradle.targets.js.yarn.YarnRootExtension

plugins {
    id 'org.jetbrains.kotlin.js' version '1.9.22'
    id 'io.gitlab.arturbosch.detekt' version '1.23.5'
}

group 'org.olafneumann'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        // used for org.jetbrains:kotlin-extensions
        url "https://maven.pkg.jetbrains.space/kotlin/p/kotlin/kotlin-js-wrappers/"
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-js"
    
    implementation "org.jetbrains.kotlinx:kotlinx-html-js:0.11.0"
    
    implementation "org.jetbrains:kotlin-extensions:1.0.1-pre.156-kotlin-1.5.0"
    
    implementation 'io.ktor:ktor-client-core:2.3.8'
    
    implementation 'io.ktor:ktor-client-js:2.3.9'
    
    // implementation 'dev.andrewbailey.difference:difference:1.0.0'
    
    implementation 'io.github.petertrr:kotlin-multiplatform-diff:0.5.0'


    implementation npm("driver.js", "0.9.8")


    testImplementation "org.jetbrains.kotlin:kotlin-test-js"
}

detekt {
    buildUponDefaultConfig = true // preconfigure defaults
    allRules = false // activate all available (even unstable) rules.
    config = files("$projectDir/.config/detekt.yml") // point to your custom config defining rules to run, overwriting default behavior
    //baseline = file("$projectDir/config/baseline.xml") // a way of suppressing issues before introducing detekt
    ignoreFailures = true
    //autoCorrect = (findProperty("detektAutoCorrect") as String?)?.toBoolean() ?: true

    reports {
        html.enabled = true // observe findings in your browser with structure and code snippets
        xml.enabled = true // checkstyle like format mainly for integrations like Jenkins
        txt.enabled = true // similar to the console output, contains issue signature to manually edit baseline files
        sarif.enabled = true // standardized SARIF format (https://sarifweb.azurewebsites.net/) to support integrations with Github Code Scanning
    }
}


kotlin {
    js(IR) {
        compileKotlinJs.kotlinOptions.moduleKind = "commonjs"
        browser {
            webpackTask {
                cssSupport {
                    enabled.set(true)
                }
            }

            runTask {
                cssSupport {
                    enabled.set(true)
                }
            }

            testTask {
                useKarma {
                    useFirefoxHeadless()
                    // webpackConfig.cssSupport.enabled = true
                }
            }
        }
        binaries.executable()
    }
}

// found here: https://youtrack.jetbrains.com/issue/KT-52578
// and here:   http://kotlin.liying-cn.net/docs/reference_zh/js/js-project-setup.html
rootProject.plugins.withType(YarnPlugin) {
  rootProject.extensions.getByType(YarnRootExtension).ignoreScripts = false
}
